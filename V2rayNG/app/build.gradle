apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion Integer.parseInt("$compileSdkVer")
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.v2ray.ang"
        minSdkVersion 17
        targetSdkVersion Integer.parseInt("$targetSdkVer")
        versionCode 55
        versionName "0.2.6"
    }

    signingConfigs {
        release {
            storeFile file("../gxyun.jks")
            keyAlias 'gxyun'
            keyPassword '111111'
            storePassword '111111'
        }
        debug {
            storeFile file("../gxyun.jks")
            keyAlias 'gxyun'
            keyPassword '111111'
            storePassword '111111'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) *
                            1000000 + android.defaultConfig.versionCode
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation project(':libv2ray')
    implementation project(':dpreference')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'com.github.jorgecastilloprz:fabprogresscircle:1.01@aar'

    // Android support library
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"

    // DSL
    implementation "org.jetbrains.anko:anko-sdk15:$ankoVersion"
    implementation "org.jetbrains.anko:anko-support-v4:$ankoVersion"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.github.pwittchen:reactivenetwork:0.12.2'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    implementation 'com.dinuscxj:recycleritemdecoration:1.0.0'
    implementation 'io.reactivex:rxkotlin:0.60.0'
    implementation 'com.orhanobut:logger:1.15'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-crash:$firebaseVersion"

    implementation 'me.dm7.barcodescanner:core:1.9.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9.3'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
    }
}

